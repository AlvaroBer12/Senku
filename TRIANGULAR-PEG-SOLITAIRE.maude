---Authors:
---Alvaro Bermudez Gamez
---Aryan Dilip Sadhwani Sadhwani

load PEG-SOLITAIRE-FIJA.maude .

mod TRIANGULAR-PEG-SOLITAIRE is
    extending FICHA-SOLITAIRE-FIJA .
    var B : Board .
    vars M N : Nat .

    op createRow : Nat -> ListaFichas .
    eq createRow(0) = nil .
    eq createRow(s M) = x createRow(M) .

    op initial : Nat -> Board .
    eq initial(0) = nilL .
    eq initial(s M) = createRow(s M) , initial(M) .

    vars v1 v2 v3 v1' v2' v3' : ListaFichas .

    crl [diagonalUpRight] : 
        (v1 o v1'),
        (v2 x v2'),
        (v3 x v3')
        =>
        (v1 x v1'),
        (v2 o v2'),
        (v3 o v3')
        if size(v1') == size(v2') /\ size(v2') == size(v3') .
    
    crl [diagonalDownRight] : 
        (v1 x v1'),
        (v2 x v2'),
        (v3 o v3')
        =>
        (v1 o v1'),
        (v2 o v2'),
        (v3 x v3')
        if size(v1) == ( size(v2) + s 0 ) /\ size(v2) == ( size(v3) + s 0 ) .
    
    crl [diagonalUpLeft] : 
        (v1 o v1'),
        (v2 x v2'),
        (v3 x v3')
        =>
        (v1 x v1'),
        (v2 o v2'),
        (v3 o v3')
        if size(v1) == ( size(v2) + s 0 ) /\ size(v2) == ( size(v3) + s 0 ) .
    
    crl [diagonalDownLeft] : 
        (v1 x v1'),
        (v2 x v2'),
        (v3 o v3')
        =>
        (v1 o v1'),
        (v2 o v2'),
        (v3 x v3')
        if size(v1') == size(v2') /\ size(v2') == size(v3') .
endm